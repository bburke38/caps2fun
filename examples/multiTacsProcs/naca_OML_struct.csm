#wing.csm similar to afrl4_wing.csm by John Dannenhoffer
#written by Sean Engelstad

#naca airfoil wing with finite differencing for mesh sensitivity

#configuration parameters
cfgpmtr  nspar  2  -
cfgpmtr  nrib  6  -

#default design parameters
despmtr  area  40  m^2
despmtr  aspect  6.0  -
despmtr  taper  0.5  -
despmtr  ctwist  0.0  deg
despmtr  lesweep  0.0 deg
despmtr  dihedral 5.0  deg
despmtr tc0 0.1
despmtr tcf 0.1
despmtr camb0 0.05
despmtr cambf 0.05

#internal parameters
set  cmean sqrt(area/aspect)
set  span cmean*aspect
set  sspan span/2
set  croot 2*cmean/(1+taper)
set  ctip croot*taper
set  xtip sspan*tand(lesweep)
set  ytip sspan*tand(dihedral)
set  margin 0.03*cmean
set  ybot -0.2*croot-margin
set  ytop 0.2*croot+ytip+margin

#make two base airfoils
udprim  naca thickness tc0 Camber camb0
scale  croot
udprim  naca thickness tcf Camber cambf
scale   ctip
rotatez		-ctwist	0		0
translate	xtip	ytip	-sspan
loft		0
attribute	OML	0

#store OML of wing
store OML
#dump	OML.egads	1

#spar parameters
set 	ctipEff		ctip*cosd(ctwist)

#spars
patbeg		ispar		nspar
   #import OML.egads	
   set		frac		0.45*ispar-0.3
   set		dx			frac*(croot-ctipEff)-xtip
   set		hyp			sqrt(sspan*sspan+dx*dx)
   box 0 ybot margin 0 ytop-ybot -hyp-2*margin
   set		localSweep	atand(dx/sspan)
   rotatey localSweep 0 0
   translate	frac*croot	0	0
   select face
      attribute capsGroup !$spar+ispar
      attribute capsMesh !$spar+ispar
      attribute spar ispar

   #we leave spars un-unioned here
patend

#ribs
patbeg		irib		nrib
   set		dxmax		max(croot,xtip+ctipEff)+2*margin
   set		zcoord		-irib*sspan/(nrib+1)
   box -2*margin ybot zcoord dxmax ytop-ybot 0
   set index irib+1
   select face
      attribute capsGroup !$rib+index
      attribute capsMesh !$rib+index

   union
   patbeg      foo  ifzero(irib-1,nspar-1,0)
      union
   patend
   #union with previous
patend 

#fillet 0.1 191

#trim ribs and spars to OML
#import OML.egads
restore OML

intersect
#import OML.egads
restore OML
hollow 0

union

#capsAIM attribute
select body
	attribute capsAIM $egadsTessAIM;tacsAIM;pointwiseAIM;fun3dAIM
	attribute capsMeshLength 1.0

dimension OMLsides 1 7
cfgpmtr OMLsides "86;72;83;60;45;34;48"

patbeg iface 7
   select face !OMLsides[1,iface]
      attribute capsGroup $OML1
      attribute capsMesh $OML1
patend

dimension OMLfaces 14 3
cfgpmtr OMLfaces "76;77;85;\
				68;67;82;\
				56;55;71;\
				41;40;59;\
				22;21;44;\
				8;7;25;\
				27;26;46;\
				75;73;84;\
				63;61;74;\
				51;49;62;\
				36;32;50;\
				16;12;33;\
				3;2;14;\
				17;15;35"

patbeg iOML 14
   patbeg iface 3
      select face !OMLfaces[iOML,iface]
      	attribute capsGroup !$OML+iOML
      	attribute capsMesh !$OML+iOML
   patend
patend

dimension rootRib 1 3
cfgpmtr rootRib "81;80;87"
dimension tipRib 1 3
cfgpmtr tipRib "29;28;47"

patbeg iface 3
   select face !rootRib[1,iface]
   	attribute capsGroup !$rib1
    attribute capsMesh !$rib1
   select face !tipRib[1,iface]
   	attribute capsGroup !$rib8
    attribute capsMesh !$rib8
patend



#capsConstraint arrays
dimension rootFaces 1 3
cfgpmtr rootFaces "81;80;87"
dimension rootEdges 1 8
cfgpmtr rootEdges "110;112;120;117;104;109;98;115"
dimension rootNodes 1 6
cfgpmtr rootNodes "52;48;47;53;50;56"

#capsConstraint and capsLoad
patbeg find 3
   select face !rootFaces[1,find]
      attribute capsConstraint $wingRoot
patend
patbeg eind 8
   select edge !rootEdges[1,eind]
      attribute capsConstraint $wingRoot
patend
patbeg nind 6
   select node !rootNodes[1,nind]
      attribute capsConstraint $wingRoot
patend
