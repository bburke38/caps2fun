#wing.csm similar to afrl4_wing.csm by John Dannenhoffer
#written by Sean Engelstad

#naca airfoil wing with finite differencing for mesh sensitivity

#default design parameters
despmtr  area  40  len^2
despmtr  aspect  6.0  -
despmtr  taper  0.5  -
despmtr  ctwist  0.0  deg
despmtr  lesweep  0.0 deg
despmtr  dihedral 5.0  deg
despmtr tc0 0.1
despmtr tcf 0.1
despmtr camb0 0.0
despmtr cambf 0.0

#internal parameters
set  cmean sqrt(area/aspect)
set  span cmean*aspect
set  sspan span/2
set  croot 2*cmean/(1+taper)
set  ctip croot*taper
set  xtip sspan*tand(lesweep)
set  ytip sspan*tand(dihedral)
set  margin 0.02*cmean
set  ybot -0.2*croot-margin
set  ytop 0.2*croot+ytip+margin

#make two base airfoils
udprim  naca thickness tc0 Camber camb0
scale  croot
udprim  naca thickness tcf Camber cambf
scale   ctip
rotatez		-ctwist	0		0
translate	xtip	ytip	-sspan
loft		0
attribute	OML	0


select face
	attribute capsGroup $OML
	attribute capsMesh $OML
	attribute capsMeshLength 0.1

store OML

set ff 20*cmean
set df 2*ff
#farfield
box -ff -ff 0 df df -ff
select face
   attribute capsGroup $Farfield
   attribute capsMesh $Farfield
   #attribute capsMeshLength 0.5
store box

restore box
restore OML
subtract

select face 3
	attribute capsGroup $Symmetry
	attribute capsMesh $Symmetry
	#attribute capsMeshLength 0.25
#capsAIM attribute
select body
	attribute capsAIM $egadsTessAIM;tacsAIM;pointwiseAIM;fun3dAIM;tetgenAIM
	attribute capsMeshLength 1.0
#restore wing