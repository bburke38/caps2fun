#wing.csm similar to afrl4_wing.csm by John Dannenhoffer
#written by Sean Engelstad

#naca airfoil wing with finite differencing for mesh sensitivity

#struct config
cfgpmtr  nspar  2  -
cfgpmtr  nrib  14  -

#default design parameters
despmtr  area  40  m^2
despmtr  aspect  6.0  -
despmtr  taper  0.5  -
despmtr  ctwist  0.0  deg
despmtr  lesweep  0.0 deg
despmtr  dihedral 5.0  deg
despmtr tc0 0.1
despmtr tcf 0.1
despmtr camb0 0.05
despmtr cambf 0.05

dimension zrib 1 15
despmtr zrib "1;1;1;1;1;1;1;1;1;1;1;1;1;1;1"



#internal parameters
set  cmean sqrt(area/aspect)
set  span cmean*aspect
set  sspan span/2
set  croot 2*cmean/(1+taper)
set  ctip croot*taper
set  xtip sspan*tand(lesweep)
set  ytip sspan*tand(dihedral)
set  margin 0.03*cmean
set  ybot -0.2*croot-margin
set  ytop 0.2*croot+ytip+margin

#make two base airfoils
udprim  naca thickness tc0 Camber camb0
scale  croot
udprim  naca thickness tcf Camber cambf
scale   ctip
rotatez		-ctwist	0		0
translate	xtip	ytip	-sspan
loft		0
attribute	OML	0

#store OML of wing
store OML
#dump	OML.egads	1

#spar parameters
set 	ctipEff		ctip*cosd(ctwist)

#spars
patbeg		ispar		nspar
   #import OML.egads	
   set		frac		0.45*ispar-0.3
   set		dx			frac*(croot-ctipEff)-xtip
   set		hyp			sqrt(sspan*sspan+dx*dx)
   box 0 ybot margin 0 ytop-ybot -hyp-2*margin
   set		localSweep	atand(dx/sspan)
   rotatey localSweep 0 0
   translate	frac*croot	0	0
   select face
      attribute capsGroup !$spar+ispar
      attribute capsMesh !$spar+ispar
      attribute spar ispar

   #we leave spars un-unioned here
patend

#ribs
set zribAdd 0
patbeg		irib		nrib
   set		dxmax		max(croot,xtip+ctipEff)+2*margin
   set zribAdd zribAdd+!zrib[1,irib]
   set zfrac zribAdd/zrib.sum
   set		zcoord		-zfrac*sspan
   box -2*margin ybot zcoord dxmax ytop-ybot 0
   set index irib
   select face
      attribute capsGroup !$rib+index
      attribute capsMesh !$rib+index

   union
   patbeg      foo  ifzero(irib-1,nspar-1,0)
      union
   patend
   #union with previous
patend 

#fillet 0.1 191

#trim ribs and spars to OML
#import OML.egads
restore OML

intersect

#make OML
restore OML
hollow 0
select face
	attribute capsGroup $OML1
	attribute capsMesh $OML1

union

#capsAIM attribute
select body
	attribute capsAIM $egadsTessAIM;tacsAIM;pointwiseAIM;fun3dAIM
	attribute capsMeshLength 1.0



#capsConstraint arrays
dimension rootFaces 1 3
cfgpmtr rootFaces "177;176;183"
dimension rootEdges 1 9
cfgpmtr rootEdges "240;238;237;232;245;226;243;249;248"
dimension rootNodes 1 7
cfgpmtr rootNodes "112;111;109;106;104;103;108"

#capsConstraint and capsLoad
patbeg find 3
   select face !rootFaces[1,find]
      attribute capsConstraint $wingRoot
patend
patbeg eind 9
   select edge !rootEdges[1,eind]
      attribute capsConstraint $wingRoot
patend
patbeg nind 7
   select node !rootNodes[1,nind]
      attribute capsConstraint $wingRoot
patend
