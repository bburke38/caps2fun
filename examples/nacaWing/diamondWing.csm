#make diamond airfoil wing for supersonic test case

#design parameters for airfoil
despmtr diamAngle0 10
despmtr chord0 1.0
despmtr diamAnglef 10

#design parameters of wing
despmtr span 6.0
despmtr AOA 0.0
despmtr taper 1.0

#config parameters
cfgpmtr nrib 10
cfgpmtr nspar 2

#wing quantities
set sspan span/2
set chordf taper*chord0
set toverc0 tand(diamAngle0)
set tovercf tand(diamAnglef)

#root airfoil quantities
set xmag0 chord0/2
set thickness0 chord0*toverc0
set ymag0 thickness0/2

mark
#make root diamond
skbeg xmag0 0 0
   linseg 0 ymag0 0
   linseg -xmag0 0 0
   linseg 0 -ymag0 0
   linseg xmag0 0 0
skend
rotatez -AOA 0 0

#tip airfoil quantities
set xmagf chordf/2
set thicknessf chordf*tovercf
set ymagf thicknessf/2

#make tip diamond
skbeg xmagf 0 -sspan
   linseg 0 ymagf -sspan
   linseg -xmagf 0 -sspan
   linseg 0 -ymagf -sspan
   linseg xmagf 0 -sspan
skend
rotatez -AOA 0 0
loft 0

store diamondLoft 0

set margin 0.1*chord0
#one bad spar originally
box 0 -ymag0-margin margin 0 2*(ymag0+margin) -sspan-2*margin

set xmmag xmag0+margin
set ymmag ymag0+margin
patbeg irib nrib
   set zpos -(irib-1)/(nrib-1)*sspan
   box -xmmag -ymmag zpos 2*xmmag 2*ymmag 0
   patbeg foo ifzero(irib-1,1,1)
      union
   patend
patend

restore diamondLoft 0
intersect

restore diamondLoft 0
hollow 0
union




   