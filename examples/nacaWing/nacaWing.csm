#fluid vs struct mesh mode
cfgpmtr cfdOn 0

#configuration parameters
cfgpmtr  nspar  2  -
cfgpmtr  nrib  16  -
cfgpmtr sharpTE  0
cfgpmtr TEfaceOn 1

#default design parameters
despmtr  area  120.0  m^2
despmtr  aspect  8.0  -
despmtr  taper  0.5  -
despmtr  ctwist  0.0  deg
despmtr  lesweep  0.0 deg
despmtr  dihedral 0.0  deg
despmtr tc0 0.1
despmtr tcf 0.1
despmtr camb0 0.05
despmtr cambf 0.05
despmtr ml0 0.4
despmtr mlf 0.4
despmtr ribSpaceFr 0.05

dimension zrib 1 nrib-1
despmtr zrib "1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1;1"

dimension xspar 1 nspar+1
despmtr xspar "0.5;1;1;1;1;1"


#internal parameters
set  cmean sqrt(area/aspect)
set  span cmean*aspect
set  sspan span/2
set  croot 2*cmean/(1+taper)
set  ctip croot*taper
set  xtip sspan*tand(lesweep)
set  ytip sspan*tand(dihedral)
set  margin 0.05*cmean
set  ybot -0.2*croot-margin
set  ytop 0.2*croot+ytip+margin
set 	ctipEff		ctip*cosd(ctwist)

#make parameters for wing loft
udprim  naca thickness tc0 Camber camb0 maxloc ml0 sharpte sharpTE
scale  croot
udprim  naca thickness tcf Camber cambf maxloc mlf sharpte sharpTE
scale   ctip
rotatez		-ctwist	0		0
translate	xtip	ytip	-sspan
loft		0
store wingloft 0

patbeg foo ifzero(cfdOn,1,0)
    #begin OML and store in wing
   udprim  naca thickness tc0 Camber camb0
   scale  croot
   #hollow 0
   udprim  naca thickness tcf Camber cambf
   scale   ctip
   rotatez		-ctwist	0		0
   translate	xtip	ytip	-sspan
   #hollow 0
    loft		0

	select face @nbody 2
		attribute capsGroup $rib1
		attribute compGroup $rib
		attribute capsConstraint $wingRoot
	select face @nbody 1
		attribute capsGroup !$rib+nrib
		attribute compGroup $rib

	select face @nbody 3
	select add @nbody 4
		attribute compGroup $OML
	#if want trailing edge face there (which you usually do), put it back
    patbeg foo ifzero(TEfaceOn-1,1,0)
        select add @nbody 5
       		attribute compGroup $OML
    patend
		
	select add @nbody 1
	select add @nbody 2
	patbeg foo ifzero(TEfaceOn,1,0)
       extract @sellist
    patend
	patbeg foo ifzero(TEfaceOn-1,1,0)
       hollow 0
    patend

	select face
	attribute	comp $wing
    store wing 0

    #make spars
    set sparsum 0
    set sparfrac 0
    patbeg		ispar		nspar
        set		sparsum   sparsum+!xspar[1,ispar]
        set      sparfrac  sparsum/xspar.sum
        set		dx			sparfrac*(croot-ctipEff)-xtip
        set		hyp			sqrt(sspan*sspan+dx*dx)
        box 0 ybot margin 0 ytop-ybot -hyp-2*margin
        set		localSweep	atand(dx/sspan)
        rotatey localSweep 0 0
        translate	sparfrac*croot	0	0
        
        select face
            attribute capsGroup !$spar+ispar
            attribute capsMesh !$spar+ispar
            attribute spar ispar
            attribute comp !$spar+ispar
            attribute compGroup $spar
    patend
#--- 
    #make inner ribs
    set zribAdd 0
    set zfrac 0
	set ninnerrib nrib-2
    patbeg		irib		ninnerrib
        set index irib+1
		set zribAdd zribAdd+!zrib[1,irib]
        set zfrac zribAdd/zrib.sum
       set xleft zfrac*xtip
       set clocal croot*(1-zfrac)+ctip*zfrac
       set xright clocal+xleft
       #make sheet box at position of rib
        set		dx  xright-xleft+margin
       set ribSpace dx*ribSpaceFr
       set   dx   dx-ribSpace
        set		zcoord		-zfrac*sspan
        box xleft-margin ybot zcoord dx ytop-ybot 0

        select face
            attribute capsGroup !$rib+index
            attribute capsMesh !$rib+index
            attribute comp !$rib+index
            attribute compGroup $rib

        patbeg foo ifzero(irib-1,nspar,1)
            union
        patend

    patend

	#intersect waffle with wingloft to make ribs and spars
    restore wingloft 0
    intersect

	#restore OML and union with ribs and spars
	restore wing 0
	union
	
	#restore wing

	set nOML nrib-1
	set OMLcolor $blue
	set ribColor $green
	set sparColor $red
	udprim editAttr filename <<
		FACE   HAS   compGroup=OML
		SET _color=red

		PATBEG  iOML nOML
           FACE  ADJ2FACE  comp=!$rib+iOML
           AND ADJ2FACE   comp=!$rib+(iOML+1)
           SET    comp=!$OML+iOML
           SET    capsGroup=!$OML+iOML
           SET    _color=!OMLcolor
        PATEND

		EDGE  ADJ2FACE  capsConstraint=wingRoot
		SET   capsConstraint=wingRoot
		NODE  ADJ2FACE  capsConstraint=wingRoot
		SET   capsConstraint=wingRoot

		FACE HAS _color=red
		AND ADJ2FACE comp=rib2
		SET  comp=OML1
		SET  capsGroup=OML1
		SET _color=!OMLcolor

		FACE HAS _color=red
		AND ADJ2FACE comp=!$rib+(nrib-1)
		SET comp=!$OML+nOML
		SET capsGroup=!$OML+nOML
		SET _color=!OMLcolor

		FACE HAS compGroup=spar
		SET _color=!sparColor
		
		FACE HAS compGroup=rib
		SET _color=!ribColor

		EDGE ADJ2FACE  compGroup=OML
		SET _color=black
	>>

	#extract @sellist

	select body
        attribute capsAIM $egadsTessAIM;tacsAIM
patend

#fluid mesh section
patbeg foo ifzero(cfdOn-1,1,0)
    set ff 20*cmean
    set df 2*ff
    #farfield
    box -ff -ff ff df df -df
    select face
    attribute capsGroup $Farfield
    attribute capsMesh $Farfield
    attribute capsMeshLength 3.0

    restore wingloft 0
    select face
        attribute capsGroup $wall
        attribute capsMesh $wall
        attribute capsMeshLength 0.1
    subtract
        #attribute capsMeshLength 1.0
    #capsAIM attribute

    select body
        attribute capsAIM $egadsTessAIM;pointwiseAIM;fun3dAIM;tetgenAIM
        attribute capsMeshLength 1.0
    
   udprim editAttr filename <<
      FACE HAS capsGroup=wall
   	  SET capsMeshLength=0.1
   
      FACE HAS capsMeshLength=Farfield
      SET capsMeshLength=3.0
   >>
#end of fluid mesh section
patend

end