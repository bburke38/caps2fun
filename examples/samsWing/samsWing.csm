cfgpmtr cfdOn 1

# DESIGN PARAMETERS
# ====================================================================================================

DESPMTR     area         3.0583
set refArea area*400.0^2.0
DESPMTR sweep_TE_i -5
DESPMTR sweep_TE_o 5

DESPMTR twist_r 1.5
DESPMTR twist_m -0.92307613
DESPMTR twist_t 0.379408659

DESPMTR taper_ratio_i 0.5
DESPMTR taper_ratio_o 0.25

DESPMTR AR 2.279574003
DESPMTR rel_y_m 0.4
DESPMTR dihedral_i -5
DESPMTR dihedral_o -5


# Nacelle 
DESPMTR inlet_area          3552
DESPMTR inlet_AR            1.0
DESPMTR engX_offset         50
DESPMTR eng_pitch_offset    0.0
DESPMTR eng_roll_offset     0.0
DESPMTR eng_min_ext         40.0
DESPMTR eng_length          385

# Landing gear length
DESPMTR LG_length           169.2

# Wing Location Reference Values
SET z_LE_r_REF              -56
SET x_TE_r_REF              1700
SET y_r_REF                 0.0

SET span        sqrt(AR*refArea)

SET y_r         y_r_REF
SET y_t         span/2
SET y_m         (y_t-y_r)*rel_y_m+y_r

SET dy_mr       y_m-y_r
SET dy_tm       y_t-y_m

SET chord_r refArea/(dy_mr*(1+taper_ratio_i)+dy_tm*taper_ratio_i*(1+taper_ratio_o))
SET chord_m chord_r*taper_ratio_i
SET chord_t chord_m*taper_ratio_o

SET x_LE_r      x_TE_r_REF-chord_r
SET x_TE_m      x_TE_r_REF+tand(sweep_TE_i)*dy_mr
SET x_LE_m      x_TE_m-chord_m
SET x_TE_t      x_TE_m+tand(sweep_TE_o)*dy_tm
SET x_LE_t      x_TE_t-chord_t

SET z_LE_r      z_LE_r_REF
SET z_TE_r      z_LE_r-(chord_r*sind(twist_r))
SET z_LE_m      z_LE_r+dy_mr*tand(dihedral_i)
SET z_TE_m      z_LE_m-(chord_m*sind(twist_m))
SET z_LE_t      z_LE_m+dy_tm*tand(dihedral_o)
SET z_TE_t      z_LE_t-(chord_t*sind(twist_t))

SET ttc_r       35.766/chord_r
SET ttc_m       11.922/chord_m
SET ttc_t       2.816/chord_t

SET sweep_LE_i  atand((x_LE_m-x_LE_r)/dy_mr)
SET sweep_LE_o  atand((x_LE_t-x_LE_m)/dy_tm)

#CALCULATE NEW AIRFOIL SECTIONS
# ..................................................................................................... 

# inboard section:

SET y_m_i       y_r_REF+0.95*dy_mr
SET chord_m_i   chord_r+((chord_m-chord_r)/dy_mr)*(y_m_i)
SET x_TE_m_i    x_TE_r_REF+tand(sweep_TE_i)*y_m_i
SET x_LE_m_i    x_TE_m_i-chord_m_i
SET z_LE_m_i    z_LE_r_REF+y_m_i*tand(dihedral_i)
SET twist_m_i   twist_r+((twist_m-twist_r)/dy_mr)*(y_m_i)
SET ttc_m_i     ttc_r+((ttc_m-ttc_r)/dy_mr)*(y_m_i)

# outboard section:

SET y_m_o       0.05*dy_tm
SET chord_m_o   chord_m+((chord_t-chord_m)/dy_tm)*(y_m_o)
SET x_TE_m_o    x_TE_m+tand(sweep_TE_o)*y_m_o
SET x_LE_m_o    x_TE_m_o-chord_m_o
SET z_LE_m_o    z_LE_m+y_m_o*tand(dihedral_o)
SET twist_m_o   twist_m+((twist_t-twist_m)/dy_tm)*(y_m_o)
SET ttc_m_o     ttc_m+((ttc_t-ttc_m)/dy_tm)*(y_m_o)
SET y_m_o       y_m_o+dy_mr

cfgpmtr nrib 10
cfgpmtr nspar 2
despmtr margin 100.0
despmtr sparmargin 0.2

MARK  # Wing -y direction

   # Root Chord
   UDPRIM biconvex thick ttc_r
   SCALE chord_r
   ROTATEZ -twist_r 0 0
   TRANSLATE  x_LE_r  z_LE_r_REF -y_r_REF 
   set x1 0
   set x2 chord_r
   set y_r -y_r_REF
   set x1 x1+x_LE_r-margin
   set x2 x2+x_LE_r+margin
   set thick1 ttc_r*chord_r+2*margin

   # Mid Chord Inboard
   UDPRIM biconvex thick ttc_m_i
   SCALE chord_m_i
   ROTATEZ -twist_m_i 0 0
   TRANSLATE x_LE_m_i z_LE_m_i -y_m_i

   # Mid Chord Inboard
   UDPRIM biconvex thick ttc_m_o
   SCALE chord_m_o
   ROTATEZ -twist_m_o 0 0
   TRANSLATE x_LE_m_o z_LE_m_o -y_m_o

   # Tip Chord
   UDPRIM biconvex thick ttc_t
   SCALE chord_t
   ROTATEZ -twist_t 0 0
   TRANSLATE  x_LE_t z_LE_t -y_t
   set x1t x_LE_t
   set x2t x1t+chord_t

   set xm_r 0.5*(x1+x2)
   set xm_t 0.5*(x1t+x2t)

BLEND
ROTATEX 90 0 0
set bodyNum @nbody
store wing

patbeg foo ifzero(cfdOn,1,0)

   MARK  # Wing -y direction

      # Root Chord
      UDPRIM biconvex thick ttc_r
      SCALE chord_r
      ROTATEZ -twist_r 0 0
      TRANSLATE  x_LE_r  z_LE_r_REF -y_r_REF 
      set x1 0
      set x2 chord_r
      set y_r -y_r_REF
      set x1 x1+x_LE_r-margin
      set x2 x2+x_LE_r+margin
      set thick1 ttc_r*chord_r+2*margin
      hollow 0

      # Mid Chord Inboard
      UDPRIM biconvex thick ttc_m_i
      SCALE chord_m_i
      ROTATEZ -twist_m_i 0 0
      TRANSLATE x_LE_m_i z_LE_m_i -y_m_i
      hollow 0

      # Mid Chord Inboard
      UDPRIM biconvex thick ttc_m_o
      SCALE chord_m_o
      ROTATEZ -twist_m_o 0 0
      TRANSLATE x_LE_m_o z_LE_m_o -y_m_o
      hollow 0

      # Tip Chord
      UDPRIM biconvex thick ttc_t
      SCALE chord_t
      ROTATEZ -twist_t 0 0
      TRANSLATE  x_LE_t z_LE_t -y_t
      set x1t x_LE_t
      set x2t x1t+chord_t
      hollow 0

      set xm_r 0.5*(x1+x2)
      set xm_t 0.5*(x1t+x2t)

   BLEND
   ROTATEX 90 0 0
   set bodyNum @nbody
   store OML

   patbeg ispar nspar
      set xfrac (ispar)/(nspar+1)
      set x_r x1*(1-xfrac)+x2*xfrac
      set x_t x1t*(1-xfrac)+x2t*xfrac
      set y1 y_r
      set y2 -y_t
      set dx x_t-x_r
      set dy y2-y1
      set x_r x_r-sparmargin*dx
      set x_t x_t+sparmargin*dx
      set y1 y1-sparmargin*dy
      set y2 y2+sparmargin*dy
      udprim waffle   filename << Depth 300
      point A AT x_r y1
      point B AT x_t y2
      line  .  A B myName=lineAB
      >>
      select face
         attribute capsGroup !$spar+ispar
         attribute capsMesh !$spar+ispar
         attribute compGroup $spar
      ROTATEX 180 0 0
   patend
   set dy y_r+y_t
   patbeg irib nrib
      set y_r -y_r_REF
      set y y_r+(irib-1)*dy/(nrib)
      set y -1*y
      set index irib
      udprim waffle   filename << Depth 300
      point A AT x1 y
      point B AT x2 y
      line  .  A B myName=lineAB
      >>
      ROTATEX 180 0 0
      select face
         attribute capsGroup !$rib+index
         attribute capsMesh !$rib+index
         attribute compGroup $rib
      patbeg foo ifzero(irib-1,nspar,1)
         union
      patend
   patend
   #store internalStruct

   restore wing
   intersect

   restore OML
   #select add @nbody 4
   #extract @sellist
   select face
      attribute capsGroup $OML
      attribute capsMesh $OML
      attribute compGroup $OML

   union

   select face
      attribute colored $false

   set ribColor $green
   set OMLcolor $blue
   set sparColor $red
   udprim editAttr filename <<
      FACE HAS compGroup=OML
      SET _color=!OMLcolor
      SET colored=true
      FACE HAS compGroup=rib
      SET color=green
      SET colored=true
      FACE HAS compGroup=spar
      SET _color=!sparColor
      SET colored=true
      FACE HAS colored=false
      SET _color=!ribColor
      SET capsGroup=rib1
      SET capsMesh=rib1
      SET compGroup=rib
      SET capsConstraint=wingRoot

      EDGE ADJ2FACE capsConstraint=wingRoot
      SET capsConstraint=wingRoot

      NODE ADJ2FACE capsConstraint=wingRoot
      SET capsConstraint=wingRoot
   >>

   select body
        attribute capsAIM $egadsTessAIM;tacsAIM
patend

despmtr ff 4000
patbeg foo ifzero(cfdOn-1,1,0)
   set df ff*2
   box -ff -ff -ff df df df
   select face
    attribute capsGroup $Farfield
    attribute capsMesh $Farfield
    attribute capsMeshLength 3.0
   translate 1000 0 0
   restore wing
   select face
        attribute capsGroup $wall
        attribute capsMesh $wall
        attribute capsMeshLength 0.1
   subtract
   select body
        attribute capsAIM $egadsTessAIM;pointwiseAIM;fun3dAIM;tetgenAIM
        attribute capsMeshLength 1.0
patend
set scaleDown 1.0/400
scale scaleDown
